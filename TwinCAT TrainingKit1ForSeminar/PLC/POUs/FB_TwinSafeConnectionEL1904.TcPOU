<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.16">
  <POU Name="FB_TwinSafeConnectionEL1904" Id="{1941b987-bdc9-4338-a5d8-d358b47e8e7b}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_TwinSafeConnectionEL1904
(*******************************************************************************************************
Description			:	Evaluate a TwinSAFE connection status and diagnosis to an EtherCAT terminal
Supported terminals : 	EL1904
Documentation 		:	EL1904 TwinSAFE input terminal with 4 fail-safe inputs
Version/Date 		:	V1.5.2/02.10.2015
Required libraries 	: 	TcSystem.lib (Tc2_System), TcEtherCAT.lib (Tc2_EtherCAT)
********************************************************************************************************)
VAR_INPUT
	
	tCoEReadCycle							:TIME 	:= T#0MS;				(* Read cycle CoE-Data, e.g. temperature *)

END_VAR
VAR_OUTPUT

	stInfoData						AT %I*	:ST_TsConnectionInfoEL1904;		(* [TwinSAFE logic] e.g. EL69xx \ Connection Info Data \ [Path] Info Data*)
	nEcSlaveState					AT %I*	:UINT ;							(* EL1904 \ InfoData \ State *)
	stEcAdsAddr						AT %I*	:ST_AdsAddr;					(* EL1904 \ InfoData \ AdsAddr *)

	stTsSlaveInfo							:ST_TsSlaveInfoEL1904;	
	
END_VAR
VAR

(* act_ConnectionDiag *)
	nDiag0_3								:BYTE;

(* act_ReadDiagObjects *)
	eReadDiagObjState						:E_TsCycleDiagObj;
	eReadDiagObjErrStep						:E_TsCycleDiagObj := E_TsCycleDiagObj.eCycleDiagObj_NoError;
	bReadDiagObjError						:BOOL;							(* Error in the diagnosis objects read cycle *)  
	bReadDiagObjReset						:BOOL;							(* Reset the diagnosis objects read cycle error *)
	fbTimer									:TON;
	fbEcCoeSdoRead							:FB_EcCoESdoReadEx;
	stDiagObjects							:ST_TsDiagObjectsEL1904;

(* act_EcSlaveIdentify *)
	fbEcGetSlaveName						:FB_EcGetSlaveName;

END_VAR
VAR CONSTANT
	
(* act_ReadDiagObjects *)
	nWarningLevelTemp1						:INT	:= 85;					(* Temperature 1 warning level [°C] *)
	nWarningLevelTemp2						:INT	:= 85;					(* Temperature 2 warning level [°C] *)

END_VAR
(* End *)]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* Function Block TwinSAFE connection status and diagnosis for EL1904 Terminals *)

(* THIS CODE IS ONLY AN EXAMPLE - YOU HAVE TO CHECK APTITUDE FOR YOUR APPLICATION *)

	(* Identify the EtherCAT slave *)
		act_EcSlaveIdentify();

	(* Evaluate the connection state *)
		act_ConnectionState();
	
	(* Evaluate the connection diagnosis *)
		act_ConnectionDiag();
	
	(* Evaluate the signals *)
		act_Signals();
	
	(* Evaluate the Terminal temperatures and diagnostic objects *)
		IF stTsSlaveInfo.bSlaveIdentify
			AND stTsSlaveInfo.bSlaveAddressOk
		THEN
			act_ReadDiagObjects();
		ELSE
			eReadDiagObjState := E_TsCycleDiagObj.eCycleDiagObj_Init;
		END_IF
		
(* End *)]]></ST>
    </Implementation>
    <Action Name="act_ConnectionDiag" Id="{2e5c9e01-f499-46c6-9013-126a602e33c9}">
      <Implementation>
        <ST><![CDATA[(* Evaluate the connection diagnosis *)

	(* Evaluate Bit 0..3 *)
	nDiag0_3 := USINT_TO_BYTE(stInfoData.nDiag) AND 16#0F;

	CASE nDiag0_3 OF
		0:		stTsSlaveInfo.sDiag := 'No error';
		1:		stTsSlaveInfo.sDiag := 'Invalid command';
		2:		stTsSlaveInfo.sDiag := 'Unknown command';
		3:		stTsSlaveInfo.sDiag := 'Invalid connection ID';
		4:		stTsSlaveInfo.sDiag := 'Invalid CRC';
		5:		stTsSlaveInfo.sDiag := 'Watchdog time elapsed';
		6:		stTsSlaveInfo.sDiag := 'Invalid FSOE address';
		7:		stTsSlaveInfo.sDiag := 'Invalid data';
		8:		stTsSlaveInfo.sDiag := 'Invalid communication parameter length';
		9:		stTsSlaveInfo.sDiag := 'Invalid communication parameters';
		10:		stTsSlaveInfo.sDiag := 'Invalid user parameter length';
		11:		stTsSlaveInfo.sDiag := 'Invalid user parameter';
		12:		stTsSlaveInfo.sDiag := 'FSoE master reset';
		13:		stTsSlaveInfo.sDiag := 'Module err detected on slave, with option "Module error is ComError" activated';
		14:		stTsSlaveInfo.sDiag := 'Module err detected on EL290x, with option "Error acknowledge active" activated';
		15:		stTsSlaveInfo.sDiag := 'Slave not yet started, or unexpected error argument';
	END_CASE

	(* Evaluate Bit 4..7 *)
	stTsSlaveInfo.sFault:='';

	IF stInfoData.nDiag.4 
	THEN
		stTsSlaveInfo.sFault := 'FSoE slave error detected - '; 
	END_IF

	IF stInfoData.nDiag.5 
	THEN
		stTsSlaveInfo.sFault := concat(stTsSlaveInfo.sFault,'FSoE slave reports Failsafe Value active - ');
	END_IF

	IF stInfoData.nDiag.6 
	THEN
		stTsSlaveInfo.sFault := concat(stTsSlaveInfo.sFault,'StartUp - ');
	END_IF

	IF stInfoData.nDiag.7 
	THEN
		stTsSlaveInfo.sFault := concat(stTsSlaveInfo.sFault, 'FSoE master reports Failsafe Value active');
	END_IF
		
	(* Error *)
	stTsSlaveInfo.bError := stInfoData.nDiag <> 0;

(* End *)]]></ST>
      </Implementation>
    </Action>
    <Action Name="act_ConnectionState" Id="{ef9bf0cf-1aeb-491e-9b78-9ce7aadcb6f4}">
      <Implementation>
        <ST><![CDATA[(* Evaluate the connection state *)

	CASE stInfoData.nState OF
		100:	stTsSlaveInfo.sState := 'Reset state';
		101:	stTsSlaveInfo.sState := 'Session state';
		102:	stTsSlaveInfo.sState := 'Connection state';
		103:	stTsSlaveInfo.sState := 'Parameter state';
		104:	stTsSlaveInfo.sState := 'Data state';
		105:	stTsSlaveInfo.sState := 'Shutdown state';
	ELSE
		stTsSlaveInfo.sState := 'unknown';
	END_CASE
	
(* End *)]]></ST>
      </Implementation>
    </Action>
    <Action Name="act_DiagObjectsBits" Id="{29ab8901-3bf7-42e3-ba34-4fc7c41ced4f}">
      <Implementation>
        <ST><![CDATA[(* act_TsDiagObjectsBits *)

	stTsSlaveInfo.stTsDiagObjectsBitsEL1904.bErrInput1		 			 := stDiagObjects.nSensorTestError.0;
	stTsSlaveInfo.stTsDiagObjectsBitsEL1904.bErrInput2		 			 := stDiagObjects.nSensorTestError.1;
	stTsSlaveInfo.stTsDiagObjectsBitsEL1904.bErrInput3		 			 := stDiagObjects.nSensorTestError.2;
	stTsSlaveInfo.stTsDiagObjectsBitsEL1904.bErrInput4		 			 := stDiagObjects.nSensorTestError.1;
	
	stTsSlaveInfo.stTsDiagObjectsBitsEL1904.bErrFirstInputPair 			 := stDiagObjects.nDuringTwoChannelEvaluationError.0;
	stTsSlaveInfo.stTsDiagObjectsBitsEL1904.bErrSecondInputPair 		 := stDiagObjects.nDuringTwoChannelEvaluationError.1;

	stTsSlaveInfo.stTsDiagObjectsBitsEL1904.bMatOpModeErrFirstInputPair	 := stDiagObjects.nMatModeInputPairDisagreeError.0 
																				AND stDiagObjects.nMatModeInputPairDisagreeError.1;
	stTsSlaveInfo.stTsDiagObjectsBitsEL1904.bMatOpModeErrSecondInputPair := stDiagObjects.nMatModeInputPairDisagreeError.2 
																				AND stDiagObjects.nMatModeInputPairDisagreeError.3;

	stTsSlaveInfo.stTsDiagObjectsBitsEL1904.bMatOpModeExtSupplyErrInput1 := stDiagObjects.nMatModeExternalSupplyError.0;
	stTsSlaveInfo.stTsDiagObjectsBitsEL1904.bMatOpModeExtSupplyErrInput2 := stDiagObjects.nMatModeExternalSupplyError.1;
	stTsSlaveInfo.stTsDiagObjectsBitsEL1904.bMatOpModeExtSupplyErrInput3 := stDiagObjects.nMatModeExternalSupplyError.2;
	stTsSlaveInfo.stTsDiagObjectsBitsEL1904.bMatOpModeExtSupplyErrInput4 := stDiagObjects.nMatModeExternalSupplyError.3;

(* End *)]]></ST>
      </Implementation>
    </Action>
    <Action Name="act_EcSlaveIdentify" Id="{c5351913-7c59-46f8-a48c-7fb42611b00b}">
      <Implementation>
        <ST><![CDATA[(* Identify the EtherCAT slave *)

	IF NOT nEcSlaveState.3
	THEN
		stTsSlaveInfo.bSlaveIdentify  	   := FALSE;
		stTsSlaveInfo.bSlaveTypeError 	   := FALSE;
		stTsSlaveInfo.sEcSlaveType         := '';
		stTsSlaveInfo.bSlaveAddressOk 	   := FALSE;
		stTsSlaveInfo.nEcSlaveFSoEAddr 	   := 0;
		stTsSlaveInfo.nEcSlaveSerialNumber := 0;
	END_IF

	IF nEcSlaveState.3
		AND NOT stTsSlaveInfo.bSlaveIdentify
		AND NOT stTsSlaveInfo.bSlaveTypeError
 		AND F_CreateAmsNetId(stEcAdsAddr.netId) <> '0.0.0.0.0.0' 
		AND stEcAdsAddr.port <> 0
	THEN

		fbEcCoeSdoRead(
			sNetId			:= 	F_CreateAmsNetId(stEcAdsAddr.netId),
			nSlaveAddr		:= 	stEcAdsAddr.port,
			nSubIndex		:= 	0,
			nIndex			:= 	16#1008,
			pDstBuf			:= 	ADR(stTsSlaveInfo.sEcSlaveType),
			cbBufLen		:= 	SIZEOF(stTsSlaveInfo.sEcSlaveType),
			bExecute		:= 	TRUE,
			tTimeout		:= 	DEFAULT_ADS_TIMEOUT);
	

		fbEcGetSlaveName(
			bExecute		:=	TRUE, 
			sNetId			:=	F_CreateAmsNetId(stEcAdsAddr.netId), 
			nSlaveAddr		:=	stEcAdsAddr.port, 
			tTimeout		:=	DEFAULT_ADS_TIMEOUT, 
			bBusy			=>	, 
			sSlaveName		=>	stTsSlaveInfo.sEcSlaveName, 
			bError			=>	, 
			nErrId			=>	);

		IF NOT fbEcCoeSdoRead.bBusy
			AND NOT fbEcCoeSdoRead.bError
			AND NOT fbEcGetSlaveName.bBusy
			AND NOT fbEcGetSlaveName.bError
		THEN

			fbEcCoeSdoRead(bExecute := FALSE); 
			fbEcGetSlaveName(bExecute := FALSE); 

			IF stTsSlaveInfo.sEcSlaveType = 'EL1904' 
				AND fbEcGetSlaveName.sSlaveName <> ''
			THEN
				stTsSlaveInfo.bSlaveIdentify := TRUE;
			ELSE
				stTsSlaveInfo.bSlaveTypeError := TRUE;
			END_IF

		ELSIF NOT fbEcCoeSdoRead.bBusy
			AND fbEcCoeSdoRead.bError
		OR (NOT fbEcGetSlaveName.bBusy
			AND fbEcGetSlaveName.bError)
		THEN
		
			fbEcCoeSdoRead  (bExecute := FALSE); 
			fbEcGetSlaveName(bExecute := FALSE); 

		END_IF

	END_IF
	

	IF nEcSlaveState.3
		AND stTsSlaveInfo.bSlaveIdentify
			AND NOT stTsSlaveInfo.bSlaveAddressOk
	THEN
		// Read slave FSoE Address  (F980:01)
		// Read slave serial number (F980:02)

			fbEcCoeSdoRead(
				sNetId			:= 	F_CreateAmsNetId(stEcAdsAddr.netId),
				nSlaveAddr		:= 	stEcAdsAddr.port,
				nSubIndex		:= 	1,
				nIndex			:= 	16#F980,
				pDstBuf			:= 	ADR(stTsSlaveInfo.stTsSlaveAddress),
				cbBufLen		:= 	SIZEOF(stTsSlaveInfo.stTsSlaveAddress),
				bExecute		:= 	TRUE,
				tTimeout		:= 	DEFAULT_ADS_TIMEOUT,
				bCompleteAccess	:= 	TRUE);

			IF NOT fbEcCoeSdoRead.bBusy 
				AND NOT fbEcCoeSdoRead.bError 
			THEN
				fbEcCoeSdoRead(bExecute := FALSE);
				stTsSlaveInfo.nEcSlaveFSoEAddr 		:= stTsSlaveInfo.stTsSlaveAddress.nEcSlaveFSoEAddr;
				stTsSlaveInfo.nEcSlaveSerialNumber 	:= TO_UDINT(stTsSlaveInfo.stTsSlaveAddress.nEcSlaveSerialNumber[0] OR SHL(TO_DWORD(stTsSlaveInfo.stTsSlaveAddress.nEcSlaveSerialNumber[1]),16));
				stTsSlaveInfo.bSlaveAddressOk 		:= TRUE;
			ELSIF NOT fbEcCoeSdoRead.bBusy 
				AND fbEcCoeSdoRead.bError 
			THEN
				fbEcCoeSdoRead(bExecute := FALSE);
				stTsSlaveInfo.nEcSlaveFSoEAddr 		:= 0;
				stTsSlaveInfo.nEcSlaveSerialNumber 	:= 0;
			END_IF	
	
	END_IF
	
(* End *)]]></ST>
      </Implementation>
    </Action>
    <Action Name="act_ReadDiagObjects" Id="{d3b99785-3285-4f96-8453-abdaab1e9480}">
      <Implementation>
        <ST><![CDATA[(* Evaluate the Terminal temperatures and diagnostic objects *)

	CASE eReadDiagObjState OF

		eCycleDiagObj_Init				:	(* Init *)	

			IF nEcSlaveState.3					(* Slave in OP *)
				AND tCoEReadCycle <> T#0MS 		(* tCoEReadCycle <> t#0ms -> Read diagnostic aktiv *)
			THEN
				bReadDiagObjError := FALSE;
				fbEcCoeSdoRead(	bExecute := FALSE);
				fbTimer(IN := FALSE);
				eReadDiagObjState := eCycleDiagObj_ReadTemperature;
			END_IF

			
		eCycleDiagObj_ReadTemperature	:	(* Read temperature *)

			fbEcCoeSdoRead(
				sNetId			:= 	F_CreateAmsNetId(stEcAdsAddr.netId),
				nSlaveAddr		:= 	stEcAdsAddr.port,
				nSubIndex		:= 	1,
				nIndex			:= 	16#FA80,
				pDstBuf			:= 	ADR(stTsSlaveInfo.stSlaveTemp),
				cbBufLen		:= 	SIZEOF(stTsSlaveInfo.stSlaveTemp),
				bExecute		:= 	TRUE,
				tTimeout		:= 	DEFAULT_ADS_TIMEOUT,
				bCompleteAccess	:= 	TRUE);

			IF NOT fbEcCoeSdoRead.bBusy 
				AND NOT fbEcCoeSdoRead.bError 
			THEN
				fbEcCoeSdoRead(bExecute := FALSE);
				eReadDiagObjState := eCycleDiagObj_ReadDiag;
			ELSIF NOT fbEcCoeSdoRead.bBusy 
				AND fbEcCoeSdoRead.bError 
			THEN
				eReadDiagObjErrStep := eReadDiagObjState;
				eReadDiagObjState 	:= eCycleDiagObj_Error;
			END_IF

			
		eCycleDiagObj_ReadDiag			:	(* Read diacnostic objects *)

			fbTimer(IN := TRUE, PT := tCoEReadCycle);

			fbEcCoeSdoRead(
				sNetId			:= 	F_CreateAmsNetId(stEcAdsAddr.netId),
				nSlaveAddr		:= 	stEcAdsAddr.port,
				nSubIndex		:= 	1,
				nIndex			:= 	16#800E,
				pDstBuf			:= 	ADR(stDiagObjects),
				cbBufLen		:= 	SIZEOF(stDiagObjects),
				bExecute		:= 	TRUE,
				tTimeout		:= 	DEFAULT_ADS_TIMEOUT,
				bCompleteAccess	:= 	TRUE);

			IF NOT fbEcCoeSdoRead.bBusy 
				AND NOT fbEcCoeSdoRead.bError 
			THEN
				fbEcCoeSdoRead(bExecute := FALSE);
				act_DiagObjectsBits();
				eReadDiagObjState := eCycleDiagObj_ReadDelay;
			ELSIF NOT fbEcCoeSdoRead.bBusy 
				AND fbEcCoeSdoRead.bError 
			THEN
				eReadDiagObjErrStep := eReadDiagObjState;
				eReadDiagObjState 	:= eCycleDiagObj_Error;
			END_IF
				
			
		eCycleDiagObj_ReadDelay		:	(* Read delay *)
			fbTimer(
				IN := TRUE, 
				PT := tCoEReadCycle);

			IF fbTimer.Q 
			THEN
				fbTimer(IN := FALSE); 			
				eReadDiagObjState := eCycleDiagObj_Done;
			END_IF

			
		eCycleDiagObj_Done				:	(* Done *)

			eReadDiagObjState := eCycleDiagObj_Init;

			
		eCycleDiagObj_Error			:	(* Error detected *)

			bReadDiagObjError := TRUE;
		
			IF bReadDiagObjReset
			THEN
				bReadDiagObjError := FALSE;
				fbEcCoeSdoRead(bExecute := FALSE);
				eReadDiagObjErrStep := E_TsCycleDiagObj.eCycleDiagObj_NoError;
				eReadDiagObjState := eCycleDiagObj_Init;
			END_IF

	ELSE
		
		eReadDiagObjState := eCycleDiagObj_Init;
		
	END_CASE

	stTsSlaveInfo.bIntTempWarning1 := stTsSlaveInfo.stSlaveTemp.nInternalTemperature1 >= nWarningLevelTemp1;
	stTsSlaveInfo.bIntTempWarning2 := stTsSlaveInfo.stSlaveTemp.nInternalTemperature2 >= nWarningLevelTemp2;
									
(* End *)]]></ST>
      </Implementation>
    </Action>
    <Action Name="act_Signals" Id="{2734c0ca-dbc4-4396-9d10-aa7bd34705b6}">
      <Implementation>
        <ST><![CDATA[(* Evaluate the input (EL1904) signals *)

	IF nEcSlaveState.3 (* Slave in operational state *)
	THEN
		(* OP -> transfer the signal states *)
		stTsSlaveInfo.bChannel1 := stInfoData.nInOutSafeDataByte1.0;
		stTsSlaveInfo.bChannel2 := stInfoData.nInOutSafeDataByte1.1;
		stTsSlaveInfo.bChannel3 := stInfoData.nInOutSafeDataByte1.2;
		stTsSlaveInfo.bChannel4 := stInfoData.nInOutSafeDataByte1.3;
	ELSE
		(* Error -> clear the signal states *)
		stTsSlaveInfo.bChannel1 := FALSE; 
		stTsSlaveInfo.bChannel2 := FALSE;
		stTsSlaveInfo.bChannel3 := FALSE; 
		stTsSlaveInfo.bChannel4 := FALSE;
	END_IF

(* End *)
]]></ST>
      </Implementation>
    </Action>
    <LineIds Name="FB_TwinSafeConnectionEL1904">
      <LineId Id="3" Count="17" />
      <LineId Id="106" Count="0" />
      <LineId Id="21" Count="5" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_TwinSafeConnectionEL1904.act_ConnectionDiag">
      <LineId Id="2" Count="49" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_TwinSafeConnectionEL1904.act_ConnectionState">
      <LineId Id="2" Count="12" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_TwinSafeConnectionEL1904.act_DiagObjectsBits">
      <LineId Id="2" Count="19" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_TwinSafeConnectionEL1904.act_EcSlaveIdentify">
      <LineId Id="2" Count="6" />
      <LineId Id="115" Count="0" />
      <LineId Id="119" Count="1" />
      <LineId Id="9" Count="34" />
      <LineId Id="125" Count="0" />
      <LineId Id="45" Count="9" />
      <LineId Id="126" Count="0" />
      <LineId Id="56" Count="11" />
      <LineId Id="69" Count="0" />
      <LineId Id="78" Count="0" />
      <LineId Id="70" Count="0" />
      <LineId Id="75" Count="0" />
      <LineId Id="82" Count="0" />
      <LineId Id="74" Count="0" />
      <LineId Id="72" Count="0" />
      <LineId Id="79" Count="0" />
      <LineId Id="83" Count="15" />
      <LineId Id="106" Count="1" />
      <LineId Id="118" Count="0" />
      <LineId Id="100" Count="2" />
      <LineId Id="105" Count="0" />
      <LineId Id="110" Count="1" />
      <LineId Id="76" Count="1" />
      <LineId Id="73" Count="0" />
      <LineId Id="68" Count="0" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_TwinSafeConnectionEL1904.act_ReadDiagObjects">
      <LineId Id="2" Count="106" />
      <LineId Id="112" Count="0" />
      <LineId Id="110" Count="0" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_TwinSafeConnectionEL1904.act_Signals">
      <LineId Id="2" Count="11" />
      <LineId Id="17" Count="2" />
      <LineId Id="14" Count="2" />
      <LineId Id="1" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>